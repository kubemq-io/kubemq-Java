plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "com.google.protobuf" version "0.9.2"
    id "java"
}
repositories {
    mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}
sourceCompatibility = JavaVersion.VERSION_1_8


dependencies {
    testImplementation group: "junit", name: "junit", version: "4.13.2"
    implementation group: "com.google.protobuf", name: "protobuf-java", version: "3.21.12"
    implementation group: "org.apache.commons", name: "commons-lang3", version: "3.12.0"
    implementation "io.grpc:grpc-alts:${grpcVersion}"
    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation group: "org.slf4j", name: "slf4j-api", version: "2.0.6"
    if (JavaVersion.current().isJava9Compatible()) {
        // Workaround for @javax.annotation.Generated
        // see: https://github.com/grpc/grpc-java/issues/3633
        implementation "javax.annotation:javax.annotation-api:1.3.2"
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.21.12"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.51.1"
        }
    }
    generateProtoTasks {
        ofSourceSet("main").configureEach {
            plugins {
                grpc {}
            }
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications{
        mavenJava(MavenPublication){
            groupId = 'io.kubemq.sdk'
            artifactId = 'kubemq-sdk-Java'
            version = '1.0.6'
            from components.java

            pom {
                name = 'io.kubemq.sdk:kubemq-sdk-Java'
                description = 'KubeMQ SDK for Java enables Java developers to easily work with KubeMQ server'
                url = 'https://docs.kubemq.io/reference/java.html'
                inceptionYear = '2019'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'Ido.Sheffer'
                        name = 'Ido.Sheffer'
                        email = 'Ido.Sheffer@kubemq.io'
                    }
                }
                scm {
                    connection = 'scm:git:git:github.com/kubemq-io/kubemq-Java.git'
                    developerConnection = 'scm:git:ssh://github.com/kubemq-io/kubemq-Java.git'
                    url = 'https://github.com/kubemq-io/kubemq-Java'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = project.properties["mavenCentralUsername"]
                password = project.properties["mavenCentralPassword"]
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}


javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}